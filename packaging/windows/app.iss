; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Qt-App"
#define MyAppVersion "0.1.1"
#define MyAppPublisher "The Youth."
#define MyAppURL "https://github.com/RealChuan/Qt-App"
#define MyAppExeName "Qt-App.exe"
#define MyAppAssocName MyAppName + ""
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt
#define MyAppUninstallName "Uninstall"
#define MyAppId "F9328618-8855-4E59-BD15-12C77B74E3C0"

#define MyCrashAppName "CrashReport"
#define MyCrashAppExeName "CrashReport.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{{#MyAppId}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
; AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
ChangesAssociations=yes
DisableProgramGroupPage=yes
LicenseFile=..\..\LICENSE
InfoBeforeFile=..\..\LICENSE
InfoAfterFile=..\..\README.md
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputDir=..\releases
OutputBaseFilename={#MyAppName}_installer
SetupIconFile=..\..\src\apps\app\app.ico
UninstallDisplayIcon={app}\{#MyAppExeName}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
MinVersion=10.0
DefaultGroupName={#MyAppName}
Uninstallable=yes
UninstallDisplayName={#MyAppName}
VersionInfoCompany={#MyAppPublisher}
VersionInfoCopyright={#MyAppPublisher}
VersionInfoDescription={#MyAppName}
VersionInfoOriginalFileName={#MyAppName}
VersionInfoProductTextVersion={#MyAppVersion}
VersionInfoProductVersion={#MyAppVersion}
VersionInfoTextVersion={#MyAppVersion}
ShowLanguageDialog=auto
; SignTool = sha256
; SignedUninstaller = yes

; "ArchitecturesAllowed=x64" specifies that Setup cannot run on
; anything but x64.
ArchitecturesAllowed=x64
; "ArchitecturesInstallIn64BitMode=x64" requests that the install be
; done in "64-bit mode" on x64, meaning it should use the native
; 64-bit Program Files directory and the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "Chinese"; MessagesFile: "ChineseSimplified.isl"
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: checkablealone
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Files]
Source: "..\packet\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion; BeforeInstall: TaskKill('{#MyAppExeName}')
Source: "..\packet\{#MyCrashAppExeName}"; DestDir: "{app}"; Flags: ignoreversion; BeforeInstall: TaskKill('{#MyCrashAppExeName}')
Source: "..\packet\fonts\*"; DestDir: "{app}\fonts"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\generic\*"; DestDir: "{app}\generic"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\iconengines\*"; DestDir: "{app}\iconengines"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\imageformats\*"; DestDir: "{app}\imageformats"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\networkinformation\*"; DestDir: "{app}\networkinformation"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\platforms\*"; DestDir: "{app}\platforms"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\plugins\*"; DestDir: "{app}\plugins"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\styles\*"; DestDir: "{app}\styles"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\tls\*"; DestDir: "{app}\tls"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\translations\*"; DestDir: "{app}\translations"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\packet\aggregation.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\crashpad_handler.exe"; DestDir: "{app}"; Flags: ignoreversion; BeforeInstall: TaskKill('crashpad_handler.exe')
Source: "..\packet\d3dcompiler_47.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\dump.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\dxcompiler.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\dxil.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\extensionsystem.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\gui.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\opengl32sw.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Core5Compat.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Gui.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Network.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Svg.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\Qt6Widgets.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\resource.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\thirdparty.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\utils.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\vc_redist.x64.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\packet\zlib1.dll"; DestDir: "{app}"; Flags: ignoreversion
; Source: "C:\Program Files (x86)\Microsoft Visual Studio\2019\Community\VC\Redist\MSVC\14.29.30133\x86\Microsoft.VC142.CRT\*"; DestDir: "{app}"; Flags: ignoreversion createallsubdirs recursesubdirs
; Source: "C:\Program Files (x86)\Windows Kits\10\Redist\10.0.19041.0\ucrt\DLLs\x86\*"; DestDir: "{app}"; Flags: ignoreversion createallsubdirs recursesubdirs

[Registry]
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: HKA; Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".myp"; ValueData: ""

Root: HKA; Subkey: "SOFTWARE\Classes\CLSID\{{{#MyAppId}}"; ValueType: string; ValueData: "{#MyAppName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "SOFTWARE\Classes\CLSID\{{{#MyAppId}}\DefaultIcon"; ValueType: string; ValueData: """{app}\{#MyAppExeName}"""; Flags: uninsdeletekey
Root: HKA; Subkey: "SOFTWARE\Classes\CLSID\{{{#MyAppId}}\shell\Open\Command"; ValueType: string; ValueData: """{app}\{#MyAppExeName}"" ""%1"""; Flags: uninsdeletekey
Root: HKA; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\MyComputer\NameSpace\{{{#MyAppId}}"; ValueType: string; ValueData: "{#MyAppName}"; Flags: uninsdeletekey

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{#MyAppUninstallName}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\vc_redist.x64.exe"; Parameters: "/q"; StatusMsg: "Installing Microsoft Visual C++ Runtime ..."; Flags: skipifdoesntexist;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""{#MyAppName}"" program=""{app}\{#MyAppExeName}"" "; Flags: runhidden;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""{#MyAppName}"" program=""{app}\{#MyAppExeName}"" dir=in action=allow enable=yes"; Flags: runhidden;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""{#MyCrashAppName}"" program=""{app}\{#MyCrashAppExeName}"" "; Flags: runhidden;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""{#MyCrashAppName}"" program=""{app}\{#MyCrashAppExeName}"" dir=in action=allow enable=yes"; Flags: runhidden;
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: filesandordirs; Name: "{app}\_"
Type: filesandordirs; Name: "{localappdata}\Youth\{#MyAppName}"
Type: filesandordirs; Name: "{localappdata}\Youth\{#MyCrashAppName}"

[Code]
procedure TaskKill(FileName: String);
var
  ResultCode: Integer;
begin
    Exec(ExpandConstant('taskkill.exe'), '/f /im ' + '"' + FileName + '"', '', SW_HIDE,
     ewWaitUntilTerminated, ResultCode);
end;
