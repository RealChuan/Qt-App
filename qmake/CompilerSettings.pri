macx{
    QMAKE_MACOSX_DEPLOYMENT_TARGET = 12.0
}

CONFIG += c++20

DEFINES += \
    QT_DEPRECATED_WARNINGS \
    QT_DISABLE_DEPRECATED_BEFORE=0x060000

# add debug info
QMAKE_CXXFLAGS_RELEASE += $$QMAKE_CFLAGS_RELEASE_WITH_DEBUGINFO
QMAKE_LFLAGS_RELEASE += $$QMAKE_LFLAGS_RELEASE_WITH_DEBUGINFO

PROJECT_NAME = Qt-App

isEmpty(RC_LANG): RC_LANG = 0x0004
isEmpty(VERSION): VERSION = 0.1.1.0
CONFIG += skip_target_version_ext

isEmpty(QMAKE_TARGET_COMPANY): QMAKE_TARGET_COMPANY = The Youth.
isEmpty(QMAKE_TARGET_DESCRIPTION): QMAKE_TARGET_DESCRIPTION = $$PROJECT_NAME
isEmpty(QMAKE_TARGET_COPYRIGHT): QMAKE_TARGET_COPYRIGHT = Copyright (C) 2017-2025 Youth.
isEmpty(QMAKE_TARGET_PRODUCT): QMAKE_TARGET_PRODUCT = $$QMAKE_TARGET_DESCRIPTION
isEmpty(QMAKE_TARGET_ORIGINAL_FILENAME): QMAKE_TARGET_ORIGINAL_FILENAME = $$QMAKE_TARGET_DESCRIPTION
isEmpty(QMAKE_TARGET_INTERNALNAME): QMAKE_TARGET_INTERNALNAME = $$QMAKE_TARGET_DESCRIPTION
isEmpty(QMAKE_TARGET_COMMENTS): QMAKE_TARGET_COMMENTS = $$QMAKE_TARGET_DESCRIPTION
isEmpty(QMAKE_TARGET_TRADEMARKS): QMAKE_TARGET_TRADEMARKS = Youth

include(ArchDetect.pri)

CONFIG(debug, debug|release) {
    RUNTIME_OUTPUT_DIRECTORY = $$PWD/../output/$$TARGET_ARCH/bin/debug
    LIBRARY_OUTPUT_DIRECTORY = $$PWD/../output/$$TARGET_ARCH/lib/debug
}else{
    RUNTIME_OUTPUT_DIRECTORY = $$PWD/../output/$$TARGET_ARCH/bin/release
    LIBRARY_OUTPUT_DIRECTORY = $$PWD/../output/$$TARGET_ARCH/lib/release
}

message("RUNTIME_OUTPUT_DIRECTORY: " + $$RUNTIME_OUTPUT_DIRECTORY)
message("LIBRARY_OUTPUT_DIRECTORY: " + $$LIBRARY_OUTPUT_DIRECTORY)

INCLUDEPATH += $$PWD/../src/
DEPENDPATH  += $$PWD/../src/
